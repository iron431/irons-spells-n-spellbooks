plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.145'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    accessTransformers {
        file('src/main/resources/META-INF/accesstransformer.cfg')
    }
}

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {
    flatDir {
        dir 'libs'
    }

    maven { url = "https://maven.enginehub.org/repo/" }
    maven {
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
        content {
            includeGroup("software.bernie.geckolib")
        }
    }
    maven { url = "https://maven.theillusivec4.top" }
    maven { url = "https://cursemaven.com" }
    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://maven.kosmx.dev/" }
}

dependencies {
    // FORGE ******************************************************************************************************
    implementation "net.neoforged:neoforge:${neo_version}"

    // GECKOLIB ***************************************************************************************************
    implementation "software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}"

    // CAELUS *****************************************************************************************************
    compileOnly "com.illusivesoulworks.caelus:caelus-neoforge:${caelus_version}:api"
    localRuntime "com.illusivesoulworks.caelus:caelus-neoforge:${caelus_version}"

    // PLAYER ANIMATOR ********************************************************************************************
    implementation "dev.kosmx.player-anim:player-animation-lib-forge:${player_animator_version}"

    // TETRA ******************************************************************************************************
    //compileOnly "se.mickelus.mutil:mutil:${mutil_version}"
    compileOnly "curse.maven:tetra-${tetra_version}"
    compileOnly "se.mickelus.mutil:mutil:${mutil_version}"

    // If you want to run with tetra in dev comment the above line and uncomment the following 2 lines
//    localRuntime "curse.maven:tetra-${tetra_version}"
//    localRuntime "se.mickelus.mutil:mutil:${mutil_version}"

    // JEI ********************************************************************************************************
    compileOnly "mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${jei_mc_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${jei_mc_version}-neoforge:${jei_version}"
    //localRuntime "curse.maven:just-enough-effect-descriptions-jeed-532286:4599099")

    // CURIOS *****************************************************************************************************
//    compileOnly "top.theillusivec4.curios:curios-forge:${curios_version}:api"
//    //Uncomment the following line to run with Curios in dev
//    localRuntime "top.theillusivec4.curios:curios-forge:${curios_version}"
    implementation "curse.maven:adorned-1036809:5533530"
//todo: 1.21: find correct maven for new curios
    // BETTER COMBAT **********************************************************************************************
//    localRuntime "curse.maven:better-combat-by-daedelus-${better_combat_version}"
//    localRuntime "curse.maven:cloth-config-348521:4633416"
//
//    // PATCHOULI **************************************************************************************************
//    compileOnly "vazkii.patchouli:Patchouli:${patchouli_version}:api"
//    localRuntime "vazkii.patchouli:Patchouli:${patchouli_version}"
//
//    // APOTHEOSIS
    compileOnly "curse.maven:apotheosis-${apotheosis_version}"
//    localRuntime "curse.maven:apotheosis-${apotheosis_version}"
//    localRuntime "curse.maven:placebo-${placebo_version}"
//    localRuntime "curse.maven:apothic-curios-950712:5108911"

    //QOL
    //todo: 1.21: update these mods
//    localRuntime "curse.maven:tslatentitystatus-tes-830420:4746241"
//    localRuntime "curse.maven:mouse-tweaks-60089:3871353"

    // JSON ******************************************************************************************************
    implementation "com.google.code.gson:gson:${gson_version}"

    // LOCAL LIBS FOR TESTING (put the jar in /libs/ folder

    //jarJar(implementation("ignored:defaultresources-forge:1.19.2-0.3.0"))) {
    //    jarJar.ranged(it, "[0.3.0,)")
    //}

    //runtimeOnly "com.refinedmods:refinedstorage:1.11.6"

    //Dimensional-Threading-Reforged-1.19.2-1.2.7.jar
    //runtimeOnly 'wearblackallday.dimthread:Dimensional-Threading-Reforged:1.19.2-1.2.7'

    //RoughlyEnoughItems-9.1.595.jar
    //    implementation "dev.architectury:architectury-forge:6.5.82"
    //    implementation "me.shedaniel.cloth:cloth-config-forge:8.2.88"
    //    implementation "me.shedaniel:RoughlyEnoughItems-forge:9.1.595"
    //    runtimeOnly 'me.shedaniel:RoughlyEnoughItems-plugin-compatibilities-forge:9.0.480'

    //valhelsia_structures-forge-1.19.2-1.0.2.jar
    //runtimeOnly 'com.stal111.valhelsia_structures:valhelsia_core-forge:1.19.2-0.5.0'
    //runtimeOnly 'com.stal111.valhelsia_structures:valhelsia_structures-forge:1.19.2-1.0.2'

    //Apotheosis
    //compileOnly "curse.maven:apotheosis-${apotheosis_version}"
    //runtimeOnly "curse.maven:apotheosis-${apotheosis_version}"
    //runtimeOnly "curse.maven:placebo-${placebo_version}"

    //Blue Skies
    //runtimeOnly 'com.legacy:blue_skies:1.19.2-1.3.20'
    //implementation "com.legacy:structure-gel:1.19.2-2.7.2"

    //implementation "io.redspace:modlist:1.0.0"
    //    for ( File file : fileTree(dir: 'libs', include: '*.jar') ) {
    //        runtimeOnly files(file)
    //    }
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_version            : mod_version,
            mod_license            : mod_license,
            mod_authors            : mod_authors,
            mod_credits            : mod_credits,
            mod_description        : mod_description,
            mod_display_url        : mod_display_url,
            mod_issue_tracker      : mod_issue_tracker,
            mod_logo_file          : mod_logo_file
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_name,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
    exclude('**/*.psd')
    exclude('**/*.bbmodel')
}

java {
    withSourcesJar()
    //withJavadocJar()
}

tasks.register('apiJar', Jar) {
    archiveClassifier.set('api')
    include 'io/redspace/ironsspellbooks/api/**/*'
    from sourceSets.main.allSource
    from sourceSets.main.output
}

artifacts {
    archives apiJar
    archives sourcesJar
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {

        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            artifact jar
            artifact sourcesJar
            artifact apiJar
        }
    }

    repositories {
        maven {
            name = "redspace"

            if (project.version.endsWith('-SNAPSHOT')) {
                url uri("https://code.redspace.io/snapshots")
            } else {
                url uri("https://code.redspace.io/releases")
            }

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASS")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.register('copyAnimations', Copy) {
    from "src/main/resources/assets/${mod_id}/player_animation"
    include 'casting_animations.json'
    into "src/main/resources/assets/${mod_id}/animations"
}

tasks.classes.dependsOn(tasks.copyAnimations)
tasks.processResources.dependsOn(tasks.copyAnimations)
tasks.sourcesJar.dependsOn(tasks.copyAnimations)

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}